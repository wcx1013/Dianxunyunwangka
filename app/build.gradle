apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

static def releaseTime() {
    return new Date().format("yyyyMMdd")
}

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.yd.dengteng"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 1
        versionName "1.0.0"

        //arouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
        }
        multiDexEnabled true
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            // 缩减resource文件
            shrinkResources false
            //Proguard
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release
            debuggable false
        }

        debug {
            shrinkResources false
            minifyEnabled false
            //签名
            signingConfig signingConfigs.release
            debuggable true
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            //这里修改apk文件名
            def fileName = "dengteng" +
                    "-vc${defaultConfig.versionCode}" +
                    "-vn${defaultConfig.versionName}" +
                    "-${releaseTime()}" +
                    "-${buildType.name}.apk"
            outputFileName = fileName
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':module_common')
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    annotationProcessor "com.alibaba:arouter-compiler:$rootProject.ext.arouterCompiler"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterknife"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.ext.glideVersion"
    implementation 'com.jpeng:JPTabBar:1.4.0'
    implementation'com.android.support.constraint:constraint-layout:1.1.0'

    implementation 'com.youth.banner:banner:1.4.10'  		// banner框架依赖
    implementation 'com.github.bumptech.glide:glide:4.9.0'		//解析图片依赖（2）
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'
}
